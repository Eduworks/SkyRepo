//Copyright 2015 Eduworks Corporation and other contributing parties.
//Licensed under an Apache 2.0 License. See LICENSE in the root directory.

type=#add(a="@",b="type");
context=#add(a="@",b="context");
owner=#add(a="@",b="owner");
reader=#add(a="@",b="reader");
protect=#add(a="@",b="protect");
obj=#toObject(obj="@obj");

owners=obj.getByParam(
	param=owner
).toArray().cforEach(
	paramName="owner",
	array="true",
	threaded="false",
	op=#replace(
		obj="@owner",
		replace="-----BEGIN PUBLIC KEY-----",
		with=""
	).replace(
		replace="-----END PUBLIC KEY-----",
		with=""
	).replace(
		regex="true",
		replace="\r?\n",
		with=""
	)
);

readers=obj.getByParam(
	param=reader
).toArray().cforEach(
	paramName="reader",
	array="true",
	threaded="false",
	op=#replace(
		obj="@reader",
		replace="-----BEGIN PUBLIC KEY-----",
		with=""
	).replace(
		replace="-----END PUBLIC KEY-----",
		with=""
	).replace(
		regex="true",
		replace="\r?\n",
		with=""
	)
);

ownersOrReaders=#object(a=owners,b=readers).valueSet().union();

urlType=#add(
	a=obj.getByParam(param=context).string(op="trim",chars="/"),
	b=#if(operator=obj.getByParam(param=context),operand="",ne="/"),
	c=#replace(obj="@type",replace=obj.getByParam(param=context),with="").string(op="trim",chars="/").debug()
);

types=obj.getByParam(param=type).toArray().cforEach(
	paramName="type",
	array="true",
	threaded="false",
	op=#if(
		operator=#has(obj="@type",has="http"),
		operand=true,
		eq=urlType,
		ne="@type"
	)
).debug();

encryptedTypes=#object(
	a="http://skyrepo.eduworks.com/EncryptedValue",
	b="EncryptedValue",
	c="http://schema.eduworks.com/ebac/0.1/encryptedValue"
).valueSet();

isEncryptedType=#if(
	operator=encryptedTypes.intersect(b=types).count(),
	operand="0",
	gt="true",
	eq=#if(
		operator=#httpGet(obj=types.getIndex(index="0")).fileToString().toObject().getByParam(param=protect),
		operand="true",
		eq="true",
		ne="false"
	)
);

filterResultObject=#if(
	operator=isEncryptedType.debug(),
	operand="true",
	eq=#if(
		operator=ownersOrReaders.debug().count(),
		operand="0",
		eq=obj.cforEach(
			paramName="key",
			valueName="obj",
			threaded="false",
			op=#filterResults()
		),
		ne=#if(
			operator=#signatureSheet().debug().intersect(b=ownersOrReaders.debug()).count(),
			operand="0",
			gt=obj.cforEach(
				paramName="key",
				valueName="obj",
				threaded="false",
				op=#filterResults()
			)
		)
	),
	ne=obj.cforEach(
		paramName="key",
		valueName="obj",
		threaded="false",
		op=#filterResults()
	)
);

filterResultArray=#toArray(obj="@obj").cforEach(
	paramName="obj",
	threaded="false",
	array="true",
	op=#filterResults()
);

filterResults=#if(
	operator="@obj",
	operand="",
	ne=#if(
		operator=#startsWith(obj="@obj",with="{"),
		operand="true",
		eq=filterResultObject,
		ne=#if(
			operator=#startsWith(obj="@obj",with="["),
			operand="true",
			eq=filterResultArray,
			ne="@obj"			
		)
	)
);

#filterResults=filterResults;